prototype(Jonnitto.PhotoSwipe:IncludeCase) < prototype(Neos.Fusion:Component) {
    alwaysInclude = ${Configuration.setting('Jonnitto.PhotoSwipe.alwaysInclude')}
    includeFetch = ${Configuration.setting('Jonnitto.PhotoSwipe.includeFetch')}
    selectorCollection = ${Configuration.setting('Jonnitto.PhotoSwipe.selector.collection')}
    selectorContent = ${Configuration.setting('Jonnitto.PhotoSwipe.selector.content')}
    selectorOnDocument = ${Configuration.setting('Jonnitto.PhotoSwipe.selector.onDocument')}

    includeInBackend = false
    node = ${documentNode}

    renderer = Neos.Fusion:Case {
        alwaysInclude {
            condition = ${props.alwaysInclude}
            renderer = true
        }
        inBackend {
            condition = ${node.context.inBackend}
            renderer = ${props.includeInBackend}
        }
        includeFetch {
            condition = ${props.includeFetch}
            renderer = true
        }
        onDocument {
            condition = ${props.selectorOnDocument && q(props.node).is(props.selectorOnDocument)}
            renderer = true
        }
        inContent {
            condition = ${props.selectorContent && q(props.node).children(props.selectorCollection).find(props.selectorContent).is()}
            renderer = true
        }
        default {
            condition = true
            renderer = false
        }
    }
}
